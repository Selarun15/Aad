---
eip: 7015
title: NFT Creator Attribution
description: Extending ERC-721 with cryptographically secured creator attribution.
author: Carlos Flores (@strollinghome)
discussions-to: https://ethereum-magicians.org/t/eip-authorship-attribution-for-erc721/14244
status: Draft
type: Standards Track
category: ERC
created: 2023-05-11
requires: 55, 155, 712, 721
---

## Abstract

This Ethereum Improvement Proposal aims to solve the issue of creator attribution for [ERC-721](./eip-721.md) Non-Fungible Tokens (NFT). To achieve this, this EIP proposes a mechanism where the NFT creator signs the required parameters for the NFT creation, including the NFT metadata and a hash of any other relevant information. The signed parameters and the signature are then validated and emitted during the deployment transaction, which allows the NFT to validate the creator and NFT platforms to attribute creatorship correctly. This method ensures that even if a different wallet sends the deployment transaction, the correct account is attributed as the creator.

## Motivation

Current NFT platforms assume that the wallet deploying the smart contract is the creator of the NFT, leading to a misattribution in cases where a different wallet sends the deployment transaction. This happens often when working with smart wallet accounts, and new contract deployment strategies such as the first collector deploying the NFT contract. This proposal aims to solve the problem by allowing creators to sign the parameters required for NFT creation so that any wallet can send the deployment transaction with an signal in a verifiable way who is the creator.

## Specification

The keywords “MUST,” “MUST NOT,” “REQUIRED,” “SHALL,” “SHALL NOT,” “SHOULD,” “SHOULD NOT,” “RECOMMENDED,” “MAY,” and “OPTIONAL” in this document are to be interpreted as described in RFC 2119.

ERC-721 compliant contracts MAY implement this NFT Creator Attribution extension to provide a standard event to be emitted that defines the NFT creator at the time of contract creation.

This EIP takes advantage of the fact that contract addresses can be precomputed before a contract is deployed. Whether the NFT contract is deployed through another contract (a factory) or through an EOA, the creator can be correctly attributed using this specification.

**Signing Mechanism**

Creator consent is given by signing an [EIP-712](./eip-712.md) compatible message; all signatures compliant with this EIP MUST include all fields defined. The struct signed is:

```solidity
struct CreatorAttribution {
	string name;
	string symbol;
	bytes32 salt;
	address token;
}
```

Where `name` and `symbol` are the ERC-721 metadata parameters, `salt` is an additional randomness parameter, and `token` is the NFTs address that will be deployed. Any extra parameters used to deploy the NFT contract MAY be hashed into the `salt` parameter.

**Signature Validation**

Creator attribution is given through a signature verification that MUST be verified by the NFT contract and an event that MUST be emitted by the NFT contract during the deployment transaction. The event includes all the necessary fields for reconstructing the signed digest and validating the signature to ensure it matches the specified creator. The event name is `CreatorAttribution` and includes the following fields:

- `name`: ERC-721Metadata name parameter
- `symbol`: ERC-721Metadata symbol parameter
- `salt`: an extra randomness parameter that encodes other relevant information for deploying the NFT contract
- `domainName`: the domain name of the contract verifying the singature (for EIP-712 signature validation)
- `version`: the version of the contract verifying the signature (for EIP-712 signature validation)
- `creator`: the account address for the creator of the NFT
- `signature`: the creator’s signature

The event is defined as follows:

```solidity
event CreatorAttribution(
	string name,               // <-------------------------------------
	string symbol,             //      Parameters signed by creator     |
	bytes32 salt,              //                                      |
	string domainName,         // <--- EIP-712 Domain Data -------------
	string version,            // <-------------------------------------
	address creator,           // <--- Creator --------------------------
	bytes signature            // <--- Creator Signature ----------------
);
```

Note that although the `chainId` parameters is necessary for [EIP-712](./eip-712.md) signatures, we omit the parameter from the event as it can be inferred through the transaction data. Similarly, the `token` parameter in the `CreatorAttribution` stuct MUST match the address of the `emitter` field of the event, hence we omit it as well.

A platform can verify the validity of the creator attribution by reconstructing the signature digest with the parameters emitted and recovering the signer from the `signature` parameter. The `creator` parameter MUST match the recovered signer from the `signature` parameter. After verification, platforms can safely attribute the `creator` address as the creator instead of the account that submitted the transaction.

### Reference Implementation

#### Example signature validator

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.19;

import "openzeppelin-contracts/contracts/utils/cryptography/EIP712.sol";
import "openzeppelin-contracts/contracts/interfaces/IERC1271.sol";

abstract contract ERC7015 is EIP712 {
    error Invalid_Signature();

    event CreatorAttribution(
        string name,
        string symbol,
        bytes32 salt,
        string domainName,
        string version,
		address creator,
        bytes signature
    );

    /// @notice Define magic value to verify smart contract signatures (ERC1271).
    bytes4 internal constant MAGIC_VALUE =
        bytes4(keccak256("isValidSignature(bytes32,bytes)"));

    bytes32 public constant TYPEHASH =
        keccak256(
            "CreatorAttribution(string name,string symbol,bytes32 salt,address token)"
        );

    constructor() EIP712("ERC7015", "1") {}

    function _validateSignature(
        string memory name,
        string memory symbol,
        bytes32 salt,
        address creator,
        bytes memory signature
    ) internal {
        if (!_isValid(name, symbol, salt, address(this), creator, signature))
            revert Invalid_Signature();

        emit CreatorAttribution(
            name,
            symbol,
            salt,
            "ERC7015",
            "1",
			creator,
            signature
        );
    }

    function _isValid(
        string memory name,
        string memory symbol,
        bytes32 salt,
        address token,
        address signer,
        bytes memory signature
    ) internal view returns (bool) {
        require(signer != address(0), "cannot validate");

        bytes32 digest = _hashTypedDataV4(
            keccak256(abi.encode(TYPEHASH, name, symbol, salt, token))
        );

        if (signer.code.length != 0) {
            try IERC1271(signer).isValidSignature(digest, signature) returns (
                bytes4 magicValue
            ) {
                return MAGIC_VALUE == magicValue;
            } catch {
                return false;
            }
        }

        address recoveredSigner = ECDSA.recover(digest, signature);

        return recoveredSigner == signer;
    }
}
```

## Rationale

By standardizing the `CreatorAttribution` event, this EIP enables platforms to ascertain creator attribution without relying on implicit assumptions. Establishing a standard for creator attribution empowers platforms to manage the complex aspects of deploying contracts while preserving accurate onchain creator information. This approach ensures a more reliable and transparent method for identifying NFT creators, fostering trust among participants in the NFT ecosystem.

[ERC-5375](./eip-5375.md) attempts to solve the same issue and although offchain data offers improved backward compatibility, ensuring accurate and immutable creator attribution is vital for NFTs. A standardized onchain method for creator attribution is inherently more reliable and secure.

In contrast to this proposal, ERC-5375 does not facilitate specifying creators for all tokens within an NFT collection, which is a prevalent practice, particularly in emerging use cases.

Both this proposal and ERC-5375 share similar limitations regarding address-based creator attribution:

> The standard defines a protocol to verify that a certain *address* provided consent. However, it does not guarantee that the address corresponds to the expected creator […]. Proving a link between an address and the entity behind it is beyond the scope of this document.

## Backwards Compatibility

Since the standard requires an event to be emitted during the NFTs deployment transaction, existing NFTs cannot implement this standard.

## Security Considerations

A potential attack exploiting this proposal could involve deceiving creators into signing creator attribution consent messages unintentionally. Consequently, creators MUST ensure that all signature fields correspond to the necessary ones before signing.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
